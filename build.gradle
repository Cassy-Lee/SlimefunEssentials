plugins {
    id 'fabric-loom' version '1.6-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

repositories {
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
    }
    maven {
        name = "Jei"
        url = "https://maven.blamejared.com/"
    }
    maven {
        name = "ModMaven"
        url = "https://modmaven.dev"
    }
    maven {
        name = "Cloth Config, Cloth Math, Rei"
        url = "https://maven.shedaniel.me/"
    }
    maven {
        name = "Jade, Just Enough Calculation"
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name = "MoreBlockPredicates"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    mavenCentral()
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation "curse.maven:jade-324717:${project.jade_version}"
    modImplementation "curse.maven:just-enough-calculation-242223:${project.jec_version}"

    modImplementation ("dev.emi:emi-fabric:${project.emi_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modImplementation("mezz.jei:jei-${project.jei_version}") {
        exclude(group: "mezz.jei")
    }

    modImplementation("maven.modrinth:mbp:${project.mbp_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modRuntimeOnly("me.shedaniel:RoughlyEnoughItems-fabric:${rei_version}")
    modCompileOnly("me.shedaniel:RoughlyEnoughItems-runtime-fabric:${rei_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modCompileOnly("me.shedaniel:RoughlyEnoughItems-api-fabric:${rei_version}")
    modCompileOnly("me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:${rei_version}")
    modCompileOnly("dev.architectury:architectury-fabric:${architectury_version}")
    modCompileOnly("me.shedaniel.cloth:basic-math:${cloth_math_version}")

    modApi "com.terraformersmc:modmenu:${project.mod_menu_version}"
    modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    compileOnly "org.projectlombok:lombok:${project.lombok_version}"
    annotationProcessor "org.projectlombok:lombok:${project.lombok_version}"
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

base {
    archivesName = project.archives_base_name
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    // TODO
}